plugins {
    id 'java-library'
    id 'maven-publish'
    // can be run to check for new versions of dependencies
    id 'com.github.ben-manes.versions' version '0.36.0'
    // test coverage
    id 'jacoco'
    id 'eclipse'
}

group 'ch.aaap'
version '1.1.2'

sourceCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'

jacoco {
    toolVersion = "0.8.0"
}
jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

test {
    useJUnitPlatform()
}
repositories {
    mavenCentral()
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://opensource.org/licenses/MIT"
        }
    }
    developers {
        developer {
            id "mnembrini"
            name "Marco Nembrini"
            email "marco.nembrini.co@gmail.com"
        }
    }

    scm {
        url "https://github.com/3AP-AG/harvest-client"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    source = sourceSets.main.allJava
    // github is set to publish this folder
    options.encoding = 'UTF-8'
    destinationDir file("docs")
    options.optionFiles << file('gradle/javadoc.options')
}

// Create the publication with the pom configuration:

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            String owner = '3ap-ag'
            String repo = 'harvest-client'
            url = uri("https://maven.pkg.github.com/${owner}/${repo}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GH_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GH_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
            // version, groupId and artifact Id taken from the project
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A Java client for the Harvest API')
                root.appendNode('name', 'harvest-client')
                root.appendNode('url', 'https://github.com/3AP-AG/harvest-client')
                root.children().last() + pomConfig
            }
        }
    }
}

ext {
    // store all version numbers that are repeated
    ver = [
        retrofit  : '2.3.0',
        junit     : '5.1.0',
        log4j     : '2.10.0',
        immutables: '2.5.6',
        okhttp    : '3.10.0',
        slf4j     : '1.7.25'
    ]
}

dependencies {

    implementation "com.squareup.retrofit2:retrofit:$ver.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$ver.retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$ver.okhttp"
    implementation "com.fatboyindustrial.gson-javatime-serialisers:gson-javatime-serialisers:1.1.1"
    // logging
    implementation "org.slf4j:slf4j-api:$ver.slf4j"

    // use log4j for unittests
    testImplementation group: "org.apache.logging.log4j", name: "log4j-core",
        version: "$ver.log4j"
    testImplementation group: "org.apache.logging.log4j", name: "log4j-slf4j-impl", version: "$ver.log4j"

    // immutable classes for domain objects
    // this will force gson 2.8 also for other dependencies
    implementation "org.immutables:gson:$ver.immutables"
    compileOnly "org.immutables:value:$ver.immutables"
    annotationProcessor "org.immutables:value:$ver.immutables"

    implementation group: "com.google.code.findbugs", name: "jsr305", version: "3.0.2"

    // configuration
    implementation "com.typesafe:config:1.3.3"

    testImplementation("org.junit.jupiter:junit-jupiter-api:$ver.junit")
    testImplementation("org.junit.jupiter:junit-jupiter-params:$ver.junit")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$ver.junit")

    testImplementation group: 'org.slf4j', name: 'jul-to-slf4j', version: "$ver.slf4j"
    testImplementation "org.mockito:mockito-core:2.15.0"
    testImplementation "org.assertj:assertj-core:3.9.1"
    testImplementation "com.squareup.okhttp3:mockwebserver:$ver.okhttp"
    testImplementation "org.assertj:assertj-joda-time:2.0.0"


}
