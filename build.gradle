// gradle plugin for junit 5
buildscript {
    repositories {
        mavenCentral()
        // The following is only necessary if you want to use SNAPSHOT releases.
        // maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0'
    }
}
plugins {
    // uploading to jcenter.bintray.com
    // see https://github.com/bintray/gradle-bintray-plugin
    id "com.jfrog.bintray" version "1.8.0"
    id "java"
    id 'java-library'
    id 'maven-publish'
    // can be run to check for new versions of dependencies
    id 'com.github.ben-manes.versions' version '0.17.0'
    // test coverage
    id 'jacoco'

    // for annotation processing
    id "net.ltgt.apt" version "0.14"
    id "net.ltgt.apt-idea" version "0.14"
    id "net.ltgt.apt-eclipse" version "0.14"
}
// plugin for junit 5
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'eclipse'

group 'ch.aaap'
version '0.2.4'

sourceCompatibility = 1.8

junitPlatform {
    details 'tree'
    // this is the default directory. Also set in circleCI
    reportsDir file('build/test-results/junit-platform')
}

// Unfortunately necessary to set a system property for the junitPlatform task
afterEvaluate {
    def junitPlatformTestTask = tasks.getByName('junitPlatformTest')
    // configure jacoco to analyze the junitPlatformTest task
    jacoco {
        // this tool version is compatible with
        toolVersion = "0.8.0"
        applyTo junitPlatformTestTask
    }

    // create junit platform jacoco task
    project.task(type: JacocoReport, "junitPlatformJacocoReport",
            {
                sourceDirectories = files("./src/main")
                classDirectories = files("$buildDir/classes/java/main")
                executionData junitPlatformTestTask
                reports {
                    xml.enabled true
                    html.enabled false
                }
            })
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.5'
}

repositories {
    mavenCentral()
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://opensource.org/licenses/MIT"
        }
    }
    developers {
        developer {
            id "mnembrini"
            name "Marco Nembrini"
            email "marco.nembrini.co@gmail.com"
        }
    }

    scm {
        url "https://github.com/3AP-AG/harvest-client"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar) {
    from javadoc.outputs
    classifier "javadoc"
}

javadoc {
    // github is set to publish this folder
    destinationDir file("docs")
    options.optionFiles << file('gradle/javadoc.options')
}
sourceSets {
    main {
        //output.generatedSourcesDir = file('src/generated')
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        all(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            // version, groupId and artifact Id taken from the project
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A Java client for the Harvest API')
                root.appendNode('name', 'harvest-client')
                root.appendNode('url', 'https://github.com/3AP-AG/harvest-client')
                root.children().last() + pomConfig
            }
        }
    }
}

ext {
    // store all version numbers that are repeated
    ver = [
            retrofit  : '2.3.0',
            junit     : '5.1.0',
            log4j     : '2.10.0',
            immutables: '2.5.6'
    ]
}

dependencies {

    compile "com.squareup.retrofit2:retrofit:$ver.retrofit"
    compile "com.squareup.retrofit2:converter-gson:$ver.retrofit"
    compile "com.squareup.okhttp3:logging-interceptor:3.10.0"
    compile "com.fatboyindustrial.gson-javatime-serialisers:gson-javatime-serialisers:1.1.1"
    // logging
    compile "org.slf4j:slf4j-api:1.7.25"
    compile group: "org.apache.logging.log4j", name: "log4j-api", version: "$ver.log4j"
    // use log4j for unittests
    runtime group: "org.apache.logging.log4j", name: "log4j-core",
            version: "$ver.log4j"
    compile group: "org.apache.logging.log4j", name: "log4j-slf4j-impl", version: "$ver.log4j"

    // immutable classes for domain objects
    // this will force gson 2.8 also for other dependencies
    compile "org.immutables:gson:$ver.immutables"
    compileOnly "org.immutables:value:$ver.immutables"
    annotationProcessor "org.immutables:value:$ver.immutables"

    compile group: "com.google.code.findbugs", name: "jsr305", version: "3.0.2"

    // configuration
    compile "com.typesafe:config:1.3.3"

    testCompile("org.junit.jupiter:junit-jupiter-api:$ver.junit")
    testCompile("org.junit.jupiter:junit-jupiter-params:$ver.junit")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:$ver.junit")

    testCompile "org.mockito:mockito-core:2.15.0"
    testCompile "org.assertj:assertj-core:3.9.1"
    compile "org.assertj:assertj-joda-time:2.0.0"


}

bintray {

    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    // what to publish
    publications = ['all']

    pkg {
        repo = '3ap'
        name = 'harvest-client'
        // TODO register 3ap on bintray
        //userOrg = '3ap'
        publish true
        licenses = ['MIT']
        vcsUrl = 'https://github.com/3AP-AG/harvest-client.git'
        version {
            name = project.version
            desc = 'Harvest Client ' + project.version
            released = new Date()
            vcsTag = 'v' + project.version

            // maven central requires gpg signing. Use the bintray key
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                // username and password on https://issues.sonatype.org/secure/Dashboard.jspa
                user = System.getenv('MAVENCENTRAL_USER')
                password = System.getenv('MAVENCENTRAL_KEY')
                //Optional property.
                // By default the staging repository is closed and artifacts are released to Maven Central.
                // You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
                close = '1'
            }
        }

    }

}
